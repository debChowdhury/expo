apply plugin: 'com.android.library'

group = 'host.exp.exponent'
version = '0.28.3'

def expoModulesCorePlugin = new File(project(":expo-modules-core").projectDir.absolutePath, "ExpoModulesCorePlugin.gradle")
apply from: expoModulesCorePlugin
applyKotlinExpoModulesCorePlugin()
useCoreDependencies()
useDefaultAndroidSdkVersions()
useExpoPublishing()

android {
  namespace "expo.modules.notifications"
  defaultConfig {
    versionCode 21
    versionName '0.28.3'
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildFeatures {
    buildConfig true
  }
  testOptions {
    unitTests.all { test ->
      testLogging {
        outputs.upToDateWhen { false }
        events "passed", "failed", "skipped", "standardError"
        showCauses true
        showExceptions true
        showStandardStreams true
      }
    }
  }
  packagingOptions {
    resources {
      // fixes "6 files found with path 'META-INF/xyz' from inputs:"
      excludes += [
              'META-INF/LICENSE.md',
              'META-INF/LICENSE-notice.md',
              'META-INF/DEPENDENCIES'
      ]
    }
  }
}

dependencies {
  api 'androidx.core:core:1.6.0'
  api 'androidx.lifecycle:lifecycle-runtime:2.2.0'
  api 'androidx.lifecycle:lifecycle-process:2.2.0'
  api 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
  api 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'


  // release notes in https://firebase.google.com/support/release-notes/android - cmd + f "Cloud Messaging version"
  api 'com.google.firebase:firebase-messaging:24.0.1'

  api 'me.leolin:ShortcutBadger:1.1.22@aar'

  if (project.findProject(':expo-modules-test-core')) {
    testImplementation project(':expo-modules-test-core')
    testImplementation 'androidx.test.ext:junit-ktx:1.2.1'

    // currently taken from expo-modules-core
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:core:1.5.0'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation "io.mockk:mockk-android:1.13.10"
    androidTestImplementation "com.google.truth:truth:1.1.2"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.0"
  }
}
